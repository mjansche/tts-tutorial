# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Copyright 2005-2011 Google, Inc.
# Author: rws@google.com (Richard Sproat)

import 'byte.grm' as bytelib;

insspace = "" : " ";

zero  = "0" : "zero";

delzero = "0" : "";

## Note that the grouping parentheses are required here:

units =   ("1" : "one")
	| ("2" : "two")
	| ("3" : "three")
	| ("4" : "four")
	| ("5" : "five")
	| ("6" : "six")
	| ("7" : "seven")
	| ("8" : "eight")
	| ("9" : "nine")
;

teens =   ("10" : "ten")
	| ("11" : "eleven")
	| ("12" : "twelve")
	| ("13" : "thirteen")
	| ("14" : "fourteen")
	| ("15" : "fifteen")
	| ("16" : "sixteen")
	| ("17" : "seventeen")
	| ("18" : "eighteen")
	| ("19" : "nineteen")
;

decades =   ("2" : "twenty")
	  | ("3" : "thirty")
	  | ("4" : "forty")
	  | ("5" : "fifty")
	  | ("6" : "sixty")
	  | ("7" : "seventy")
	  | ("8" : "eighty")
	  | ("9" : "ninety")
;

numbers_20_to_99 = decades delzero | decades insspace units ;

numbers_to_99 = Optimize[units | teens | numbers_20_to_99] ;

hundreds = units insspace ("" : "hundred") ;

numbers_100_to_999 = 
Optimize[
  hundreds delzero delzero |
  hundreds delzero insspace units |
  hundreds insspace teens |
  hundreds insspace numbers_20_to_99]
;

numbers_to_999 = Optimize[numbers_to_99 | numbers_100_to_999];

digit = bytelib.kDigit;

two_digit_numbers = teens | decades delzero | decades insspace units;

# An approximation to years --- doesn't get "2001" right, for example.

year = ((("1" | "2") digit) @ two_digit_numbers)
       insspace
       ((digit digit) @ two_digit_numbers
        | ("0" : "oh") insspace (digit @ units));

read_digit_by_digit = (units | zero) (insspace (units | zero))* ;

# This must be exported to be available to other grammars. Note the use of
# a weight in the rule to indicate deprecation of the read_digit_by_digit rule
# if there are other matches:

export NUMBERS = Optimize[numbers_to_999
                          | year
                          | (read_digit_by_digit <1.0>)] ;
